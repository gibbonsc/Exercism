module RunLengthEncoding

let encode (input: string ) : string =
    match input.Length with
    | 0 | 1 -> input
    | _ ->

        let charList =
            input.ToCharArray()
            |> Array.toList

        let examine
            (
                (acc : string,  // accumulated parsed tokens
                runner : char,  // current runner charaacter
                counter : int)  // run counter so far
            )  // collected accumulated state between examines
            (c : char)  // specimen
            : (string * char * int) =

            match c = runner with
                | true ->
                    // still running the same character
                    (acc, runner,counter + 1)
                | _ ->
                    // different character:
                    let token = (
                        if counter=1
                        then ""
                        else string counter) + (string runner)
                    (acc+token, c, 1)

        let (result : string, finalRunner: char, finalCounter: int) =
            List.fold examine ("", charList.Head, 1) charList.Tail

        let finalCountString = 
            (if finalCounter=1 then "" else string finalCounter)
        result + finalCountString + (string finalRunner)

let decode (input : string) : string =
    match input.Length with
    | 0 | 1 -> input
    | _ ->
        let charList =
            input.ToCharArray()
            |> Array.toList
        let examine
            (
                (acc: string,  // accumulated expanded tokens
                counterString: string)  // accumulated digits
            )  // collected state between examines
            (c : char)
            : (string * string) =  //

            match c with
                | '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9' ->
                    // digit: adjust counterString
                    acc, counterString + (string c)
                | _ ->
                    // non digit: expand completed token
                    let runCount : int = (
                        if counterString=""
                        then 1
                        else (int counterString)
                        )
                    acc + String.replicate runCount (string c), ""
        let (result, _) = List.fold examine ("", "") charList
        result
